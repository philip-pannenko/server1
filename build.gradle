plugins {
    id "com.palantir.docker" version "0.20.1"
    id "java"
    id "idea"
    id "org.springframework.boot" version "2.1.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.9.0"
    id 'org.liquibase.gradle' version '2.0.1'
}

group 'net.pannenko.cicd'
version '1.0.0'

bootJar {
    baseName = 'server1'
    version = '1.0.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    //maven {
    //    url "http://localhost:8081/artifactory/jcenter"
    //}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.5'
    compile group: 'net.pannenko.cicd', name: 'server-utils', version: '1.0.0'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.SR2"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

artifactory {
    contextUrl = 'http://localhost:8081/artifactory'
    publish {
        repository {
            repoKey = 'gradle-dev' // The Artifactory repository key to publish to
            username = "admin" // The publisher user name
            password = "Password1!" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('maven')
            publishArtifacts = true


        }
    }
}


task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/db/changelog.xml'
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'root'
        }
    }
}

